apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "java"
apply plugin: 'application'

version = '1.0'
mainClassName = "de.uni_bremen.factroytrouble.gui.FactoryTroubleApp"

jar {
    manifest {
        attributes 'Main-Class': "de.uni_bremen.factroytrouble.gui.FactoryTroubleApp",
                'Implementation-Version': version,
                'Implementation-Title': 'FactoryTrouble'
    }
}

ext {
    appName = "FactoryTrouble"
}

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/groups/public"
    }

}
dependencies {
    compile project(":gameEngine")
    compile project(":api")
    compile project(":ai")
    compile project(":spring")
    compile files('libs/jfoenix.jar')

    testCompile 'de.saxsys:jfx-testrunner:1.0'
}


buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.edu.sc.seis:macAppBundle:2.1.5"
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.1'
        classpath 'de.undercouch:gradle-download-task:2.1.0'
    }

}

apply plugin: "edu.sc.seis.macAppBundle"
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'de.undercouch.download'

macAppBundle {
    icon = "appContent/icon.icns"
    appName = "FactoryTrouble"
    mainClassName = "de.uni_bremen.factroytrouble.gui.FactoryTroubleApp"
    bundleJRE = true
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
}

launch4j {
    mainClassName = 'de.uni_bremen.factroytrouble.gui.FactoryTroubleApp'
    //icon = 'appContent/icon.ico'
    appName = "FactoryTrouble"
    outfile = 'FactoryTrouble.exe'
}

task zipWinExe(type: Zip) {
    from "build/launch4j"
    exclude '*launch4j*'
    baseName = 'FactoryTroubleWin'
}

task downloadJFoenixJAR << {
    download {
        src 'http://www.jfoenix.com/download/jfoenix.jar'
        dest "libs/jfoenix.jar"
    }
}

build.dependsOn downloadJFoenixJAR
compileJava.dependsOn downloadJFoenixJAR
eclipseClasspath.dependsOn downloadJFoenixJAR